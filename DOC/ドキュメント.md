# T# ドキュメント

## Introduction
T＃は、ソフトウェアを構築するために設計されたスタックベースのプログラミング言語です。
T＃はPorth、Forthに似ています。

## Install & Run
```bash
$ git clone https://github.com/Tsharp-lang/Tsharp
$ cd tsharp
$ go build main.go
$ ./main examples/main.t#
or
$ ./main.exe examples/main.t#
```

## Hello World
```pascal
"Hello 世界!" print
```

'print' スタックの一番上の要素を print してスタックから削除します。

## コメント
```python
# Sample comment
```

## インポート
```python
import "main.t#"
```

## Block
```pascal
block main do
    "Hello 世界!" print
end

call main
```

'block' は他の言語でいう関数みたいなもの


## If文
```pascal
if false do
    "Hello 世界!" print
elif false do
    "elif body!" print
else
    "else body!" print
end

10 10 == print
20 10 != print
2 10 < print
10 2 > print
```

## Dup
```pascal
"Hello World" dup print print
```
'dup' スタックの一番上の要素を複製してスタックに入れます。

## Drop
```pascal
"Hello 世界!" "T# Programming Language" drop print
```
'drop' はスタックの一番上の要素を削除します。

## PrintS
```python
1 2 "Hello World!"

printS

# stack length  
#     ↓ 
#    <3>  1 2 'Hello World' <- top
```
'printS' スタックの中身を全てprint。printした値はスタックからdropされません。

## PrintV
```python
10 -> N
"Hello World!" -> text

printV

# N : 10
# text : "Hello World!"

```
'printV' 変数を全てprint。

## For loop
```pascal
for true do
    "Hello 世界!" print
    break
end
```

## 計算
```pascal
34 35 + print

100 40 - print

200 5 / print

10 2 * print
```

## 変数
```pascal
10 -> x

x -> y

y print
```

## Type
```python
int # 12345
string # "Hello World!"
bool # true false
type # int string bool type
```

## Typeof
```python
"Hello World" dup typeof print
```

## Rot
```python
1 2 3 rot print print print
```
'rot' は、上位3つのスタック要素を回転します

## Over
```python
1 2 over print print print
```
'over' はスタックの一番下にある要素をコピーします

## 文字列結合
```python
"Hello " "World!" + print 
```

## インクリメント
```python
1 inc print
```

## デクリメント
```python
10 dec print
```

## Exit
```python
"Hello World"
exit
print
```
'exit' はプログラムを強制終了させます。

## リスト
```python
[] # 空のリストをスタックに入れる

"Hello World!" append # 文字列 "Hello World!" をリストに append

34 append # 数値 34 をリストに append

68 append # 数値 68 をリストに append

"T# Programming Language" 1 replace # リスト インデックス '1' を文字列 "T# Programming Language" に置き換えます

print
```

## FizzBuzz
```pascal
1
for dup 100 <= do
    if dup 15 % 0 == do
        "FizzBuzz" print
    elif dup 3 % 0 == do
        "Fizz" print
    elif dup 5 % 0 == do
        "Buzz" print
    else
        dup print
    end
    inc
end drop
```
